{
  description = "Fully featured flake â„ï¸ for rusty ğŸ¦€ development";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";

    rust-overlay = {
      url = "github:oxalica/rust-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    flake-parts.url = "github:hercules-ci/flake-parts";

    treefmt-nix = {
      url = "github:numtide/treefmt-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    inputs@{ flake-parts, ... }:
    flake-parts.lib.mkFlake { inherit inputs; } {
      systems = [
        "aarch64-darwin"
        "aarch64-linux"
        "x86_64-darwin"
        "x86_64-linux"
      ];

      imports = [ inputs.treefmt-nix.flakeModule ];

      perSystem =
        { pkgs, system, ... }:
        {
          _module.args.pkgs = import inputs.nixpkgs {
            inherit system;
            overlays = [ (import inputs.rust-overlay) ];
          };

          treefmt.programs = {
            deadnix.enable = true;
            statix.enable = true;
            nixfmt = {
              enable = true;
              strict = true;
            };

            prettier.enable = true;

            shfmt.enable = true;
            shellcheck.enable = true;

            rustfmt.enable = true;

            taplo.enable = true;
          };

          packages.default = pkgs.callPackage ./package.nix { };

          devShells.default = pkgs.mkShell {
            packages = with pkgs; [
              # Nix lsp â„ï¸
              nil

              # For the extra reinforcement ğŸ˜‚
              cargo-mommy

              # file watcher ğŸ‘€
              bacon

              # Next gen testing ğŸ§ª
              cargo-nextest
              cargo-flamegraph
              cargo-mutants
              cargo-tarpaulin

              # License ğŸ“œ
              cargo-deny

              # supply chain â›“ï¸
              cargo-vet
              cargo-auditable
              cargo-crev

              # Dependencies ğŸ“¦
              cargo-udeps
              cargo-machete

              # Tasks ğŸ› ï¸
              cargo-make
              cargo-chef
              cargo-cross

              # Unsafe â˜¢ï¸
              cargo-geiger

              # Inner workings âš™ï¸
              cargo-show-asm
              cargo-expand

              # misc â”
              cargo-msrv
              cargo-release
            ];

            nativeBuildInputs = [ (pkgs.rust-bin.fromRustupToolchainFile ./rust-toolchain.toml) ];
          };
        };
    };
}
